import java.nio.charset.StandardCharsets

plugins {
	// IDE
	id 'idea'

	// Language
	id 'java'
	id 'java-library'

	// Publishing
	id 'com.vanniktech.maven.publish' version '0.35.0'

	// Utility
	id 'jacoco'
	id 'info.solidsoft.pitest' version '1.15.0'
	id 'com.diffplug.spotless' version '8.1.0'
	id 'org.sonarqube' version '7.1.0.6387'
}

group = "$GROUP"
version = "$VERSION_NAME"
description = "$POM_DESCRIPTION"

repositories {
	mavenCentral()
}

dependencies {
	// https://mvnrepository.com/artifact/jakarta.inject/jakarta.inject-api
	api group: 'jakarta.inject', name: 'jakarta.inject-api', version: jakartaVersion

	// https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: jUnitVersion

	// https://mvnrepository.com/artifact/nl.jqno.equalsverifier/equalsverifier-nodep
	testImplementation group: 'nl.jqno.equalsverifier', name: 'equalsverifier-nodep', version: equalsVerifierVersion
}

test {
	useJUnitPlatform()
}

java {
	// Aligned with the version of Java Jakarta dependency is built with
	def version = JavaVersion.VERSION_17

	sourceCompatibility = version.toString()
	targetCompatibility = version.toString()

	toolchain {
		languageVersion = JavaLanguageVersion.of(version.toString())
	}
}

// Configure several tasks additionally for Gradle
test {
	useJUnitPlatform()

	// Report is always generated after tests run
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test

	reports {
		html.required = true
		xml.required = true
		csv.required = false
	}
}

// Enable Spotless code formatting rules
spotless {
	java {
		target '**/src/*/java/**/*.java'

		// Aligns with Intellij IDEA default settings
		toggleOffOn('@formatter:off', '@formatter:on')

		googleJavaFormat()
	}

	groovyGradle {
		target '**/*.gradle'

		greclipse()
	}
}

// Configure several tasks additionally for Gradle
tasks.withType(Copy).configureEach {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = StandardCharsets.UTF_8.name()
	dependsOn(spotlessJavaCheck)
}

pitest {
	targetClasses = ['io.github.suppierk.*']
	threads = 4
	outputFormats = ['HTML']
	timestampedReports = false
	junit5PluginVersion = '1.2.1'
}

sonar {
	properties {
		property 'sonar.projectKey', 'SuppieRK_inject'
		property 'sonar.organization', 'suppierk'
		property 'sonar.host.url', 'https://sonarcloud.io'
	}
}